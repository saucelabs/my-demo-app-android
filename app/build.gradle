apply plugin: 'com.android.application'
apply plugin: 'jacoco'

android {
    signingConfigs {}

    compileSdk 34
    useLibrary 'org.apache.http.legacy'
    ndkVersion '21.3.6528147'

    defaultConfig {
        applicationId "com.saucelabs.mydemoapp.android"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1 // CI automatically replaces this
        versionName "1.0.0" // CI automatically replaces this
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        buildConfigField "String", "SAUCE_USERNAME", getProperty("SAUCE_USERNAME", "\"\"")
        buildConfigField "String", "SAUCE_ACCESS_KEY", getProperty("SAUCE_ACCESS_KEY", "\"\"")
    }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources true
        }
    }

    buildTypes {
        debug {
            debuggable true
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    namespace 'com.saucelabs.mydemoapp.android'

    lintOptions {
        checkReleaseBuilds false
    }

    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildFeatures {
        buildConfig = true
    }
}

String getProperty(String name, String defaultValue) {
    if (!rootProject.file("./local.properties").exists()) {
        return defaultValue
    }
    def properties = new Properties()
    properties.load(rootProject.file("./local.properties").newDataInputStream())
    return properties.containsKey(name) ? properties[name] : defaultValue
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${project.buildDir}/intermediates/javac/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.from(files([mainSrc]))
    classDirectories.from(files([debugTree]))

    executionData.from(fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ]))
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Libraries For Testing
    androidTestImplementation("com.saucelabs.visual:visual-espresso:0.0.1")

    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:3.0.2")
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.biometric:biometric:1.1.0"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'com.google.android.material:material:1.4.0'

    def room_version = "2.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    //Retrofit
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.guava:guava:33.0.0-android'

    // Utils
    implementation 'com.github.gcacace:signature-pad:1.3.1'
    implementation(name:'com.kyanogen.signatureview.signature-view', ext:'aar')
    implementation(name:'com.williamww.silky-signature', ext:'aar')
    implementation(name:'com.uphyca.creditcardedittext', ext:'aar')

    implementation 'com.github.GrenderG:Toasty:1.4.2'
    implementation "com.squareup.okhttp3:okhttp:4.9.0"

    // Backtrace SDK
    implementation 'com.github.backtrace-labs.backtrace-android:backtrace-library:3.7.0'

    def camerax_version = "1.1.0"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "com.google.zxing:core:3.3.3"

    configurations.implementation {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
    }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
